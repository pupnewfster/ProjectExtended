buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

plugins {
    id 'java'
    id 'idea'
    id "com.wynprice.cursemaven" version "2.1.1"
}

idea {
    module {
        // For some reason this is necessary for IDEA 2014 workspaces to recognize assets
        inheritOutputDirs = true
        //Exclude directories from being managed
        for (String excludeDirName in ["run", "out", "logs", "gradle"]) {
            File excludeDir = new File(projectDir, excludeDirName)
            excludeDirs.add(excludeDir)
        }
    }
}

apply plugin: 'net.minecraftforge.gradle'

sourceSets {
    main {
        resources {
            srcDirs += 'src/generated/resources'
            //There is no need to pack the Data Generator cache into the build
            exclude '.cache'
        }
    }
    datagen {
        //Let the data gen module know about the main module
        compileClasspath += sourceSets.main.output
    }
}

configurations {
    //Let the datagen source set know about forge
    datagenCompile.extendsFrom(compile)
}

group = 'gg.galaxygaming.projectextended'
version = "${minecraft_version}-${mod_version}"
archivesBaseName = 'ProjectExtended'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    mappings channel: "snapshot", version: "${mappings_version}"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            properties 'fml.earlyprogresswindow': 'false'

            //The below if statements are to add args to your gradle.properties file in user home
            // (DO NOT add them directly to the gradle.properties file for this project)
            // Setting the below properties allows use of your normal Minecraft account in the
            // dev environment including having your skin load. Each property also has a comment
            // explaining what information to set the value to/format it expects
            // One thing to note is because of the caching that goes on, after changing these
            // variables, you need to refresh the project and rerun genIntellijRuns/genEclipseRuns
            if (project.hasProperty('mc_uuid')) {
                //Your uuid without any dashes in the middle
                args '--uuid', project.getProperty('mc_uuid')
            }
            if (project.hasProperty('mc_username')) {
                //Your username/display name, this is the name that shows up in chat
                // Note: This is not your email, even if you have a Mojang account
                args '--username', project.getProperty('mc_username')
            }
            if (project.hasProperty('mc_accessToken')) {
                //Your access token, you can find it in your '.minecraft/launcher_profiles.json' file
                args '--accessToken', project.getProperty('mc_accessToken')
            }

            mods {
                projectextended {
                    source(sourceSets.main)
                }
            }
        }

        server {
            workingDirectory project.file('run')
            mods {
                projectextended {
                    source(sourceSets.main)
                }
            }
        }

        data {
            workingDirectory project.file('run')
            environment 'target', 'fmluserdevdata'
            properties 'fml.earlyprogresswindow': 'false'


            args '--mod', 'projectextended', '--all', '--output', '"' + file('src/generated/resources/') + '"',
                    '--existing', '"' + sourceSets.main.resources.srcDirs[0] + '"',
                    //Fake the fact that the dm png exists
                    '--existing', '"' + sourceSets.datagen.resources.srcDirs[0] + '"'
            mods {
                projectextended {
                    sources(sourceSets.main, sourceSets.datagen)
                }
            }
        }
    }
}

repositories {
    maven {
        name 'JEI'
        url "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    //Include JEI in the dev environment
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")
    compile fg.deobf("curse.maven:projecte:${projecte_fileid}")
}

task replaceResources(type: Copy) {
    outputs.upToDateWhen { false }
    from (sourceSets.main.resources) {
        include "META-INF/mods.toml"
        expand "version": mod_version, "mc_version": minecraft_version, "forge_version": min_forge_version, "loader_version": loader_version, "projecte_version": projecte_version
    }
    into "$buildDir/resources/main/"
}

processResources {
    //Exclude the mods.toml file as we manually handle that and don't want it to invalidate our cache
    exclude 'META-INF/mods.toml'
    finalizedBy(replaceResources)
}